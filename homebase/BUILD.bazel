load("@npm_homebase//@bazel/typescript:index.bzl", "ts_library")
load("@npm_homebase//@bazel/rollup:index.bzl", "rollup_bundle")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("@io_bazel_rules_docker//container:container.bzl", "container_image")
load("@io_bazel_rules_k8s//k8s:object.bzl", "k8s_object")
load("@io_bazel_rules_k8s//k8s:objects.bzl", "k8s_objects")
load("//tools:k8s.bzl", "k8s_http_service", "k8s_virtual_host")

exports_files(
    ["tsconfig.json"],
    visibility = ["//visibility:public"],
)

ts_library(
    name = "homebase",
    srcs = [
        "App.tsx",
        "index.tsx",
    ],
    tsconfig = "//homebase:tsconfig.json",
    deps = [
        "//homebase/components",
        "//homebase/go-links/components",
        "//homebase/homepage/components",
        "//homebase/trips/components",
        "@npm_homebase//:node_modules",
    ],
)

rollup_bundle(
    name = "bundle",
    config_file = "rollup.config.js",
    entry_point = "index.tsx",
    format = "umd",
    link_workspace_root = True,
    output_dir = True,
    deps = [
        ":homebase",
        "//homebase/styles",
        "@npm_homebase//:node_modules",
    ],
)

sh_library(
    name = "server_lib",
    data = [
        "homebase-dev.caddy",
        ":bundle",
    ],
)

sh_binary(
    name = "server",
    srcs = ["caddy-dev.sh"],
    data = [
        ":server_lib",
    ],
    deps = ["@bazel_tools//tools/bash/runfiles"],
)

pkg_tar(
    name = "pkg",
    srcs = [
        ":bundle",
    ],
    mode = "0644",
    package_dir = "./homebase",
    strip_prefix = "./bundle",
)

container_image(
    name = "image",
    base = "@caddy//image",
    files = [
        "homebase.caddy",
    ],
    tags = ["no-cache"],
    tars = [
        ":pkg",
    ],
)

k8s_object(
    name = "k8s_deploy",
    images = {
        "index.docker.io/mmoriarity/homebase-srv:latest": ":image",
    },
    kind = "deployment",
    template = "deployment.yaml",
)

k8s_http_service(
    name = "k8s_service",
    app = "homebase",
    namespace = "homebase",
    service_name = "homebase-srv",
)

k8s_object(
    name = "k8s_ingress",
    template = "ingress.yaml",
)

k8s_objects(
    name = "k8s",
    objects = [
        ":k8s_deploy",
        ":k8s_service",
        ":k8s_ingress",
        "//homebase/cmd/homebase-bot-srv:k8s",
    ],
    tags = ["manual"],
    visibility = ["//visibility:public"],
)
